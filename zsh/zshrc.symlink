#zmodload zsh/zprof

# shortcut to this dotfiles path is $ZSH
export ZSH=$HOME/dev/home/dotfiles

# your project folder that we can `c [tab]` to
export PROJECTS=~/dev

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($ZSH/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  # EXCLUDING OH-MY-ZSH!!
  if [[ "$file" != *oh-my-zsh* ]] 
  then
    source $file
  fi
done

# initialize autocomplete here, otherwise functions won't be loaded
# only check zcompdump to recompute once a day
autoload -Uz compinit 
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
	compinit;
else
	compinit -C;
fi;

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

# Now source oh-my-zsh
source ~/.oh-my-zshrc

# #stty -ixon

# spaceship prompt
SPACESHIP_MAVEN_SHOW=false
SPACESHIP_GRADLE_SHOW=false

#zprof

# opencode
export PATH=/home/collin/.opencode/bin:$PATH

# bun
export BUN_INSTALL="$HOME/.bun"
export PATH="$BUN_INSTALL/bin:$PATH"
